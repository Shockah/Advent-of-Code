buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		google()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

apply plugin: "kotlin"

group = "com.github.Shockah"

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}
	maven {
		url "https://oss.sonatype.org/content/repositories/releases/"
	}
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
	api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
	api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
	api 'com.github.Shockah:Unikorn:50324242c6705f521727bed39321a18b31819be0'
	api 'com.github.Shockah:Jay:1.3'

	api 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

sourceSets.main {
	kotlin.srcDirs = ["src"]
	resources.srcDirs = ["resources"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier = "sources"
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "1.8"
		freeCompilerArgs = ["-XXLanguage:+InlineClasses", "-Xuse-experimental=kotlin.Experimental"]
	}
}

task(solve, dependsOn: 'classes', type: JavaExec) {
	group = 'run'
	mainClass = 'pl.shockah.aoc.AdventSolver'
	classpath = sourceSets.main.runtimeClasspath
}

task(validate, dependsOn: 'classes', type: JavaExec) {
	group = 'run'
	mainClass = 'pl.shockah.aoc.AdventValidator'
	classpath = sourceSets.main.runtimeClasspath
}

tasks.withType(JavaExec) {
	if (System.getProperty('DEBUG', 'false') == 'true') {
		jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
	}
}